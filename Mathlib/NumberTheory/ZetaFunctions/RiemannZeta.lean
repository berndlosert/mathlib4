/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.Gamma.Beta
import Mathlib.NumberTheory.ModularForms.JacobiTheta.OneVariable
import Mathlib.NumberTheory.ZetaFunctions.AbstractFuncEq
import Mathlib.NumberTheory.ZetaValues

#align_import number_theory.zeta_function from "leanprover-community/mathlib"@"57f9349f2fe19d2de7207e99b0341808d977cdcf"

/-!
# Definition of the Riemann zeta function

## Main definitions:

* `riemannZeta`: the Riemann zeta function `Î¶ : â„‚ â†’ â„‚`.
* `riemannCompletedZeta`: the completed zeta function `Î› : â„‚ â†’ â„‚`, which satisfies
  `Î›(s) = Ï€ ^ (-s / 2) Î“(s / 2) Î¶(s)` (away from the poles of `Î“(s / 2)`).
* `riemannCompletedZetaâ‚€`: the entire function `Î›â‚€` satisfying
  `Î›â‚€(s) = Î›(s) + 1 / (s - 1) - 1 / s` wherever the RHS is defined.

Note that mathematically `Î¶(s)` is undefined at `s = 1`, while `Î›(s)` is undefined at both `s = 0`
and `s = 1`. Our construction assigns some values at these points (which are not arbitrary, but
I haven't checked exactly what they are).

## Main results:

* `differentiable_completedZetaâ‚€` : the function `Î›â‚€(s)` is entire.
* `differentiableAt_completedZeta` : the function `Î›(s)` is differentiable away from `s = 0` and
  `s = 1`.
* `differentiableAt_riemannZeta` : the function `Î¶(s)` is differentiable away from `s = 1`.
* `zeta_eq_tsum_one_div_nat_add_one_cpow` : for `1 < re s`, we have
  `Î¶(s) = âˆ‘' (n : â„•), 1 / (n + 1) ^ s`.
* `riemannCompletedZetaâ‚€_one_sub`, `riemannCompletedZeta_one_sub`, and `riemannZeta_one_sub` :
  functional equation relating values at `s` and `1 - s`
* `riemannZeta_neg_nat_eq_bernoulli` : for any `k âˆˆ â„•` we have the formula
  `riemannZeta (-k) = (-1) ^ k * bernoulli (k + 1) / (k + 1)`
* `riemannZeta_two_mul_nat`: formula for `Î¶(2 * k)` for `k âˆˆ â„•, k â‰  0` in terms of Bernoulli
  numbers

## Outline of proofs:

We define a function `zetaKernel` on the reals, given by `t â†¦ Î˜ (t * I)` where `Î¸` is Jacobi's
theta function. This satisfies a functional equation relating its values at `t` and `1 / t`, and
has the form `1 + O(t ^ (-r))` for every `r` as `t â†’ âˆ`. Thus its Mellin transform has meromorphic
continuation and satisfies a functional equation, by general theory.

On the other hand, since `zetaKernel` can be expanded in powers of `exp (-Ï€ * t)` and the Mellin
transform integrated term-by-term for `1 < Re s`, we obtain the relation to the naive Dirichlet
series `âˆ‘' (n : â„•), 1 / (n + 1) ^ s`.
-/


open MeasureTheory Set Filter Asymptotics TopologicalSpace Real Asymptotics

open Complex hiding exp norm_eq_abs abs_of_nonneg abs_two continuous_exp

open scoped Topology Real Nat

noncomputable section

/-!
## Definition of the completed Riemann zeta
-/

/-- The Jacobi theta function along the imaginary axis. -/
def zetaKernel (t : â„) : â„‚ := jacobiTheta (t * I)

lemma continuousAt_zetaKernel {t : â„} (ht : 0 < t) : ContinuousAt zetaKernel t := by
  refine (continuousAt_jacobiTheta ?_).comp (continuous_ofReal.continuousAt.mul_const _)
  rwa [mul_I_im, ofReal_re]

lemma locallyIntegrableOn_zetaKernel : LocallyIntegrableOn zetaKernel (Ioi 0) := by
  apply ContinuousOn.locallyIntegrableOn ?_ measurableSet_Ioi
  exact fun t ht â†¦ (continuousAt_zetaKernel ht).continuousWithinAt

lemma zetaKernel_one_div (t : â„) (ht : 0 < t) :
    zetaKernel (1 / t) = (1 * â†‘(t ^ (1 / 2 : â„)) : â„‚) â€¢ zetaKernel t := by
  -- the following is basically trivial, but greatly obscured by the distinction between
  -- `rpow` and `cpow` etc
  have ht' : 0 < (1 / t * I).im := by rwa [mul_I_im, one_div, â† ofReal_inv, ofReal_re, inv_pos]
  simp_rw [one_mul, ofReal_cpow ht.le, ofReal_div, ofReal_one, zetaKernel,
    jacobiTheta_eq_jacobiThetaâ‚‚, ofReal_div, ofReal_one, ]
  have := jacobiThetaâ‚‚_functional_equation 0 ht'
  rw [zero_div, (by norm_num : (1 / 2 : â„‚) = â†‘(1 / 2 : â„))] at this
  simp_rw [this, zero_pow (by norm_num : 0 < 2), mul_zero, zero_div, Complex.exp_zero, mul_one,
    neg_div, one_div (1 / t * I), mul_inv, one_div (t : â„‚), inv_inv, â† mul_neg, inv_I, neg_neg]
  congr 1
  rw [mul_comm, mul_assoc, mul_neg, I_mul_I, neg_neg, mul_one, â† ofReal_inv, â† ofReal_cpow
    (inv_pos.mpr ht).le, inv_rpow ht.le, ofReal_inv, one_div, inv_inv, ofReal_cpow ht.le,
    ofReal_div, ofReal_one]

lemma zetaKernel_sub_one_isBigO (r : â„) :
    (zetaKernel Â· - 1) =O[atTop] fun x â†¦ x ^ (-r) := by
  have h' : Tendsto (fun t : â„ â†¦ t * I) atTop (comap im atTop) := by
    rw [tendsto_comap_iff]
    convert tendsto_id using 1
    ext1 t
    rw [Function.comp_apply, mul_I_im, ofReal_re, id.def]
  refine (isBigO_at_im_infty_jacobiTheta_sub_one.comp_tendsto h').trans ?_
  simp_rw [Function.comp_def, mul_I_im, ofReal_re]
  exact (isLittleO_exp_neg_mul_rpow_atTop pi_pos (-r)).isBigO

/-- The zeta kernel forms a `WeakFEPair` with itself. -/
def zetaKernelFEPair : WeakFEPair â„‚ where
  f       := zetaKernel
  g       := zetaKernel
  k       := 1 / 2
  Îµ       := 1
  fâ‚€      := 1
  gâ‚€      := 1
  hk      := by norm_num
  hÎµ      := by norm_num
  hf_int  := locallyIntegrableOn_zetaKernel
  hg_int  := locallyIntegrableOn_zetaKernel
  h_feq   := zetaKernel_one_div
  hf_top  := zetaKernel_sub_one_isBigO
  hg_top  := zetaKernel_sub_one_isBigO

/-- The completed Riemann zeta function with its poles removed, `Î›(s) + 1 / s - 1 / (s - 1)`. -/
def riemannCompletedZetaâ‚€ (s : â„‚) : â„‚ := (zetaKernelFEPair.Î›â‚€ (s / 2)) / 2
#align riemann_completed_zetaâ‚€ riemannCompletedZetaâ‚€

/-- The completed Riemann zeta function, `Î›(s)`, which satisfies
`Î›(s) = Ï€ ^ (-s / 2) Î“(s / 2) Î¶(s)` (up to a minor correction at `s = 0`). -/
def riemannCompletedZeta (s : â„‚) : â„‚ := zetaKernelFEPair.Î› (s / 2) / 2
#align riemann_completed_zeta riemannCompletedZeta

lemma riemannCompletedZeta_eq (s : â„‚) :
    riemannCompletedZeta s = riemannCompletedZetaâ‚€ s - 1 / s - 1 / (1 - s) := by
  rw [sub_sub, eq_sub_iff_add_eq]
  have := zetaKernelFEPair.Î›â‚€_eq (s / 2)
  simp_rw [riemannCompletedZetaâ‚€, riemannCompletedZeta, this, add_div, add_assoc, smul_eq_mul]
  congr 2
  Â· change 1 / s = 1 / (s / 2) * 1 / 2
    rw [mul_one, div_div, div_mul_cancel _ two_ne_zero]
  Â· change 1 / (1 - s) = 1 / (â†‘(1 / 2 : â„) - s / 2) * 1 / 2
    rw [mul_one, ofReal_div, ofReal_one, ofReal_ofNat, div_div, sub_mul,
      div_mul_cancel _ two_ne_zero, div_mul_cancel _ two_ne_zero]

theorem completedZeta_eq_mellin_of_one_lt_re {s : â„‚} (hs : 1 < re s) :
    riemannCompletedZeta s = mellin (fun t â†¦ (zetaKernel t - 1) / 2) (s / 2) := by
  rw [riemannCompletedZeta, mellin_div_const, div_left_inj' two_ne_zero]
  refine (zetaKernelFEPair.hasMellin (?_ : 1 / 2 < re (s / 2))).2.symm
  simp_rw [â† ofReal_ofNat, div_ofReal]
  exact div_lt_div_of_lt two_pos hs
#align completed_zeta_eq_mellin_of_one_lt_re completedZeta_eq_mellin_of_one_lt_re

/-- The modified completed Riemann zeta function `Î›(s) + 1 / s + 1 / (1 - s)` is entire. -/
theorem differentiable_completedZetaâ‚€ : Differentiable â„‚ riemannCompletedZetaâ‚€ :=
  (zetaKernelFEPair.differentiable_Î›â‚€.comp (differentiable_id.div_const _)).div_const _
#align differentiable_completed_zetaâ‚€ differentiable_completedZetaâ‚€

/-- The completed Riemann zeta function `Î›(s)` is differentiable away from `s = 0` and `s = 1`. -/
theorem differentiableAt_completedZeta {s : â„‚} (hs : s â‰  0) (hs' : s â‰  1) :
    DifferentiableAt â„‚ riemannCompletedZeta s := by
  refine ((zetaKernelFEPair.differentiableAt_Î› (div_ne_zero hs two_ne_zero)
    (?_ : s / 2 â‰  â†‘(1 / 2 : â„))).comp s (differentiableAt_id.div_const _)).div_const _
  rw [ofReal_div, ofReal_one, ofReal_ofNat]
  exact hs' âˆ˜ (div_left_inj' two_ne_zero).mp
#align differentiable_at_completed_zeta differentiableAt_completedZeta

/-- Riemann zeta functional equation, formulated for `Î›â‚€`: for any complex `s` we have
`Î›â‚€(1 - s) = Î›â‚€ s`. -/
theorem riemannCompletedZetaâ‚€_one_sub (s : â„‚) :
    riemannCompletedZetaâ‚€ (1 - s) = riemannCompletedZetaâ‚€ s := by
  rw [riemannCompletedZetaâ‚€, riemannCompletedZetaâ‚€, sub_div,
    (by norm_num : (1 / 2 : â„‚) = â†‘(1 / 2 : â„)), (by rfl : (1 / 2 : â„) = zetaKernelFEPair.k),
    zetaKernelFEPair.functional_equationâ‚€ (s / 2)]
  simp only [zetaKernelFEPair, WeakFEPair.symm, one_div, inv_one, one_smul]
#align riemann_completed_zetaâ‚€_one_sub riemannCompletedZetaâ‚€_one_sub

/-- Riemann zeta functional equation, formulated for `Î›`: for any complex `s` we have
`Î› (1 - s) = Î› s`. -/
theorem riemannCompletedZeta_one_sub (s : â„‚) :
    riemannCompletedZeta (1 - s) = riemannCompletedZeta s := by
  simp_rw [riemannCompletedZeta_eq, riemannCompletedZetaâ‚€_one_sub, sub_sub_cancel, sub_right_comm]
#align riemann_completed_zeta_one_sub riemannCompletedZeta_one_sub

/-!
## The un-completed Riemann zeta function
-/

/-- The Riemann zeta function `Î¶(s)`. We set this to be irreducible to hide messy implementation
details. -/
irreducible_def riemannZeta :=
  Function.update (fun s â†¦ (Ï€ : â„‚) ^ (s / 2) * riemannCompletedZeta s / Gamma (s / 2)) 0 (-1 / 2)
#align riemann_zeta riemannZeta

/- Note the next lemma is true by definition; what's hard is to show that with this definition, `Î¶`
is continuous (and indeed analytic) at 0, see `differentiableAt_riemannZeta` below. -/
/-- We have `Î¶(0) = -1 / 2`. -/
theorem riemannZeta_zero : riemannZeta 0 = -1 / 2 := by
  rw [riemannZeta]
  exact Function.update_same _ _ _
#align riemann_zeta_zero riemannZeta_zero

/-- The Riemann zeta function is differentiable away from `s = 1`. -/
theorem differentiableAt_riemannZeta {s : â„‚} (hs' : s â‰  1) : DifferentiableAt â„‚ riemannZeta s := by
  /- First claim: the result holds at `t` for `t â‰  0`. Note we will need to use this for the case
    `s = 0` also, as a hypothesis for the removable-singularity criterion. -/
  have c1 (t : â„‚) (ht : t â‰  0) (ht' : t â‰  1) : DifferentiableAt â„‚
        (fun u : â„‚ â†¦ (Ï€ : â„‚) ^ (u / 2) * riemannCompletedZeta u / Gamma (u / 2)) t := by
    apply DifferentiableAt.mul
    Â· refine (DifferentiableAt.const_cpow ?_ ?_).mul (differentiableAt_completedZeta ht ht')
      Â· exact differentiableAt_id.div_const  _
      Â· exact Or.inl (ofReal_ne_zero.mpr pi_pos.ne')
    Â· exact differentiable_one_div_Gamma.differentiableAt.comp t (differentiableAt_id.div_const  _)
  -- Second claim: the limit at `s = 0` exists and is equal to `-1 / 2`.
  have c2 : Tendsto (fun s : â„‚ â†¦ (Ï€ : â„‚) ^ (s / 2) * riemannCompletedZeta s / Gamma (s / 2))
      (ğ“[â‰ ] 0) (ğ“ <| -1 / 2) := by
    have h1 : Tendsto (fun z : â„‚ â†¦ (Ï€ : â„‚) ^ (z / 2)) (ğ“ 0) (ğ“ 1) := by
      convert (ContinuousAt.comp (f := fun z â†¦ z/2)
        (continuousAt_const_cpow (ofReal_ne_zero.mpr pi_pos.ne')) ?_).tendsto using 2
      Â· simp_rw [Function.comp_apply, zero_div, cpow_zero]
      Â· exact continuousAt_id.div continuousAt_const two_ne_zero
    suffices h2 : Tendsto (fun z â†¦ riemannCompletedZeta z / Gamma (z / 2)) (ğ“[â‰ ] 0) (ğ“ <| -1 / 2)
    Â· convert (h1.mono_left nhdsWithin_le_nhds).mul h2 using 1
      Â· ext1 x; rw [mul_div]
      Â· simp only [one_mul]
    suffices h3 :
      Tendsto (fun z â†¦ riemannCompletedZeta z * (z / 2) / (z / 2 * Gamma (z / 2))) (ğ“[â‰ ] 0)
        (ğ“ <| -1 / 2)
    Â· refine Tendsto.congr' (eventuallyEq_of_mem self_mem_nhdsWithin fun z hz â†¦ ?_) h3
      rw [â† div_div, mul_div_cancel _ (div_ne_zero hz two_ne_zero)]
    have h4 : Tendsto (fun z : â„‚ â†¦ z / 2 * Gamma (z / 2)) (ğ“[â‰ ] 0) (ğ“ 1) := by
      refine tendsto_self_mul_Gamma_nhds_zero.comp ?_
      rw [tendsto_nhdsWithin_iff, (by simp : ğ“ (0 : â„‚) = ğ“ (0 / 2))]
      exact
        âŸ¨(tendsto_id.div_const _).mono_left nhdsWithin_le_nhds,
          eventually_of_mem self_mem_nhdsWithin fun x hx â†¦ div_ne_zero hx two_ne_zeroâŸ©
    suffices Tendsto (fun z â†¦ riemannCompletedZeta z * z / 2) (ğ“[â‰ ] 0) (ğ“ (-1 / 2 : â„‚)) by
      have := this.div h4 one_ne_zero
      simp_rw [div_one, mul_div_assoc] at this
      exact this
    refine Tendsto.div ?_ tendsto_const_nhds two_ne_zero
    simp_rw [riemannCompletedZeta_eq, sub_mul]
    rw [show ğ“ (-1 : â„‚) = ğ“ (0 - 1 - 0) by norm_num]
    refine (Tendsto.sub ?_ ?_).sub ?_
    Â· refine Tendsto.mono_left ?_ nhdsWithin_le_nhds
      have : ContinuousAt riemannCompletedZetaâ‚€ 0 :=
        differentiable_completedZetaâ‚€.continuous.continuousAt
      simpa only [id.def, mul_zero] using Tendsto.mul this tendsto_id
    Â· refine tendsto_const_nhds.congr' (eventuallyEq_of_mem self_mem_nhdsWithin fun t ht â†¦ ?_)
      simp_rw [one_div_mul_cancel ht]
    Â· refine Tendsto.mono_left ?_ nhdsWithin_le_nhds
      suffices ContinuousAt (fun z : â„‚ â†¦ 1 / (1 - z)) 0 by
        simpa only [id.def, mul_zero] using Tendsto.mul this tendsto_id
      exact continuousAt_const.div (continuousAt_const.sub continuousAt_id) (by norm_num)
  -- Now the main proof.
  rcases ne_or_eq s 0 with (hs | rfl)
  Â· -- The easy case: `s â‰  0`
    have : {(0 : â„‚)}á¶œ âˆˆ ğ“ s := isOpen_compl_singleton.mem_nhds hs
    refine (c1 s hs hs').congr_of_eventuallyEq (eventuallyEq_of_mem this fun x hx â†¦ ?_)
    rw [riemannZeta]
    apply Function.update_noteq hx
  Â· -- The hard case: `s = 0`.
    rw [riemannZeta, â† (lim_eq_iff âŸ¨-1 / 2, c2âŸ©).mpr c2]
    have S_nhds : {(1 : â„‚)}á¶œ âˆˆ ğ“ (0 : â„‚) := isOpen_compl_singleton.mem_nhds hs'
    refine ((Complex.differentiableOn_update_limUnder_of_isLittleO S_nhds
        (fun t ht â†¦ (c1 t ht.2 ht.1).differentiableWithinAt) ?_) 0 hs').differentiableAt S_nhds
    simp only [zero_div, div_zero, Complex.Gamma_zero, mul_zero, cpow_zero, sub_zero]
    -- Remains to show completed zeta is `o (s ^ (-1))` near 0.
    refine (isBigO_const_of_tendsto c2 <| one_ne_zero' â„‚).trans_isLittleO ?_
    rw [isLittleO_iff_tendsto']
    Â· exact Tendsto.congr (fun x â†¦ by rw [â† one_div, one_div_one_div]) nhdsWithin_le_nhds
    Â· exact eventually_of_mem self_mem_nhdsWithin fun x hx hx' â†¦ (hx <| inv_eq_zero.mp hx').elim
#align differentiable_at_riemann_zeta differentiableAt_riemannZeta

/-- The trivial zeroes of the zeta function. -/
theorem riemannZeta_neg_two_mul_nat_add_one (n : â„•) : riemannZeta (-2 * (n + 1)) = 0 := by
  have : (-2 : â„‚) * (n + 1) â‰  0 :=
    mul_ne_zero (neg_ne_zero.mpr two_ne_zero) (Nat.cast_add_one_ne_zero n)
  rw [riemannZeta, Function.update_noteq this,
    show -2 * ((n : â„‚) + 1) / 2 = -â†‘(n + 1) by push_cast; ring, Complex.Gamma_neg_nat_eq_zero,
    div_zero]
#align riemann_zeta_neg_two_mul_nat_add_one riemannZeta_neg_two_mul_nat_add_one

/-- Riemann zeta functional equation, formulated for `Î¶`: if `1 - s âˆ‰ â„•`, then we have
`Î¶ (1 - s) = 2 ^ (1 - s) * Ï€ ^ (-s) * Î“ s * sin (Ï€ * (1 - s) / 2) * Î¶ s`. -/
theorem riemannZeta_one_sub {s : â„‚} (hs : âˆ€ n : â„•, s â‰  -n) (hs' : s â‰  1) :
    riemannZeta (1 - s) =
      (2 : â„‚) ^ (1 - s) * (Ï€ : â„‚) ^ (-s) * Gamma s * sin (Ï€ * (1 - s) / 2) * riemannZeta s := by
  -- Deducing this from the previous formulations is quite involved. The proof uses two
  -- nontrivial facts (the doubling formula and reflection formula for Gamma) and a lot of careful
  -- rearrangement, requiring several non-vanishing statements as input to `field_simp`.
  have hs_ne : s â‰  0 := by contrapose! hs; rw [hs]; exact âŸ¨0, by rw [Nat.cast_zero, neg_zero]âŸ©
  have h_sqrt : (sqrt Ï€ : â„‚) â‰  0 := ofReal_ne_zero.mpr (sqrt_ne_zero'.mpr pi_pos)
  have h_pow : (2 : â„‚) ^ (s - 1) â‰  0 := by
    rw [Ne.def, cpow_eq_zero_iff, not_and_or]
    exact Or.inl two_ne_zero
  have h_Ga_ne1 : Gamma (s / 2) â‰  0 := by
    rw [Ne.def, Complex.Gamma_eq_zero_iff]
    contrapose! hs
    obtain âŸ¨m, hmâŸ© := hs
    rw [div_eq_iff (two_ne_zero' â„‚), â† Nat.cast_two, neg_mul, â† Nat.cast_mul] at hm
    exact âŸ¨m * 2, by rw [hm]âŸ©
  have h_Ga_eq : Gamma s = Gamma (s / 2) * Gamma ((s + 1) / 2) * (2 : â„‚) ^ (s - 1) / sqrt Ï€ := by
    rw [add_div, Complex.Gamma_mul_Gamma_add_half, mul_div_cancel' _ (two_ne_zero' â„‚),
      (by ring : 1 - s = -(s - 1)), cpow_neg, â† div_eq_mul_inv, eq_div_iff h_sqrt,
      div_mul_eq_mul_divâ‚€, div_mul_cancel _ h_pow]
  have h_Ga_ne3 : Gamma ((s + 1) / 2) â‰  0 := by
    have h_Ga_aux : Gamma s â‰  0 := Complex.Gamma_ne_zero hs
    contrapose! h_Ga_aux
    rw [h_Ga_eq, h_Ga_aux, mul_zero, zero_mul, zero_div]
  rw [riemannZeta, Function.update_noteq (by rwa [sub_ne_zero, ne_comm] : 1 - s â‰  0),
    Function.update_noteq hs_ne, riemannCompletedZeta_one_sub, mul_div, eq_div_iff h_Ga_ne1,
    mul_comm, â† mul_div_assoc]
  -- Now rule out case of s = positive odd integer & deduce further non-vanishing statements
  by_cases hs_pos_odd : âˆƒ n : â„•, s = 1 + 2 * n
  Â· -- Note the case n = 0 (i.e. s = 1) works OK here, but only because we have used
    -- `Function.update_noteq` to change the goal; the original goal is genuinely false for s = 1.
    obtain âŸ¨n, rflâŸ© := hs_pos_odd
    have : (1 - (1 + 2 * (n : â„‚))) / 2 = -â†‘n := by
      rw [â† sub_sub, sub_self, zero_sub, neg_div, mul_div_cancel_left _ (two_ne_zero' â„‚)]
    rw [this, Complex.Gamma_neg_nat_eq_zero, div_zero]
    have : (Ï€ : â„‚) * (1 - (1 + 2 * â†‘n)) / 2 = â†‘(-n : â„¤) * Ï€ := by push_cast; field_simp; ring
    rw [this, Complex.sin_int_mul_pi, mul_zero, zero_mul]
  have h_Ga_ne4 : Gamma ((1 - s) / 2) â‰  0 := by
    rw [Ne.def, Complex.Gamma_eq_zero_iff]
    contrapose! hs_pos_odd
    obtain âŸ¨m, hmâŸ© := hs_pos_odd
    rw [div_eq_iff (two_ne_zero' â„‚), sub_eq_iff_eq_add, neg_mul, â† sub_eq_neg_add,
      eq_sub_iff_add_eq] at hm
    exact âŸ¨m, by rw [â† hm, mul_comm]âŸ©
  -- At last the main proof
  rw [show sin (â†‘Ï€ * (1 - s) / 2) = Ï€ * (Gamma ((1 - s) / 2) * Gamma (s / 2 + 1 / 2))â»Â¹ by
      have := congr_arg Inv.inv (Complex.Gamma_mul_Gamma_one_sub ((1 - s) / 2)).symm
      rwa [(by ring : 1 - (1 - s) / 2 = s / 2 + 1 / 2), inv_div,
        div_eq_iff (ofReal_ne_zero.mpr pi_pos.ne'), mul_comm _ (Ï€ : â„‚), mul_div_assoc'] at this]
  rw [(by rw [â† neg_sub] : (2 : â„‚) ^ (1 - s) = (2 : â„‚) ^ (-(s - 1))), cpow_neg, h_Ga_eq]
  suffices (Ï€ : â„‚) ^ ((1 - s) / 2) = (Ï€ : â„‚) ^ (-s) * sqrt Ï€ * (Ï€ : â„‚) ^ (s / 2) by
    rw [this]; field_simp;
    ring_nf; rw [â† ofReal_pow, sq_sqrt pi_pos.le]; ring
  simp_rw [sqrt_eq_rpow, ofReal_cpow pi_pos.le, â† cpow_add _ _ (ofReal_ne_zero.mpr pi_pos.ne')]
  congr 1
  push_cast
  field_simp
  ring
#align riemann_zeta_one_sub riemannZeta_one_sub

/-- A formal statement of the **Riemann hypothesis** â€“ constructing a term of this type is worth a
million dollars. -/
def RiemannHypothesis : Prop :=
  âˆ€ (s : â„‚) (_ : riemannZeta s = 0) (_ : Â¬âˆƒ n : â„•, s = -2 * (n + 1)) (_ : s â‰  1), s.re = 1 / 2
#align riemann_hypothesis RiemannHypothesis


/-!
##Â Relating the Mellin transform to the Dirichlet series
-/

/-- Auxiliary lemma for `mellin_zetaKernelâ‚_eq_tsum`, computing the Mellin transform of an
individual term in the series. -/
theorem integral_cpow_mul_exp_neg_pi_mul_sq {s : â„‚} (hs : 0 < s.re) (n : â„•) :
    âˆ« t : â„ in Ioi 0, (t : â„‚) ^ (s - 1) * rexp (-Ï€ * t * ((n : â„) + 1) ^ 2) =
      (Ï€ : â„‚) ^ (-s) * Complex.Gamma s * (1 / ((n : â„‚) + 1) ^ (2 * s)) := by
  rw [Complex.Gamma_eq_integral hs, GammaIntegral_eq_mellin]
  conv_rhs =>
    congr
    rw [â† smul_eq_mul, â† mellin_comp_mul_left _ _ pi_pos]
  have : 1 / ((n : â„‚) + 1) ^ (2 * s) = (((n + 1) ^ (2 : â„) : â„) : â„‚) ^ (-s) := by
    rw [(by norm_num : (n : â„‚) + 1 = â†‘((n : â„) + 1)), (by norm_num : 2 * s = â†‘(2 : â„) * s),
      cpow_mul_ofReal_nonneg, one_div, cpow_neg]
    rw [â† Nat.cast_succ]
    exact Nat.cast_nonneg _
  conv_rhs => rw [this, mul_comm, â† smul_eq_mul]
  rw [â† mellin_comp_mul_right _ _ (show 0 < ((n : â„) + 1) ^ (2 : â„) by positivity)]
  refine set_integral_congr measurableSet_Ioi fun t _ â†¦ ?_
  simp_rw [smul_eq_mul]
  congr 3
  conv_rhs => rw [â† Nat.cast_two, rpow_nat_cast]
  ring
#align integral_cpow_mul_exp_neg_pi_mul_sq integral_cpow_mul_exp_neg_pi_mul_sq

/-- Express `zetaKernel - 1` as a sum over `â„•`. -/
theorem zetaKernel_sub_one_eq_tsum_nat {t : â„} (ht : 0 < t) :
    zetaKernel t - 1 = 2 * âˆ‘' n : â„•, rexp (-Ï€ * t * ((n : â„) + 1) ^ 2) := by
  rw [zetaKernel, jacobiTheta_eq_tsum_nat ((mul_I_im t).symm â–¸ ht : 0 < (â†‘t * I).im),
    add_comm, add_sub_cancel, mul_right_inj' two_ne_zero]
  push_cast
  congr 2 with n
  rw [(by ring : â†‘Ï€ * I * ((n : â„‚) + 1) ^ 2 * (t * I) = I ^ 2 * Ï€ * t * ((n : â„‚) + 1) ^ 2),
    I_sq, neg_one_mul]
#align zeta_kernelâ‚_eq_jacobi_theta zetaKernel_sub_one_eq_tsum_nat

/-- The sum for `zetaKernel` is convergent. -/
theorem summable_exp_neg_pi_mul_nat_sq {t : â„} (ht : 0 < t) :
    Summable fun n : â„• â†¦ rexp (-Ï€ * t * ((n : â„) + 1) ^ 2) := by
  have : 0 < (â†‘t * I).im := by rwa [im_ofReal_mul, I_im, mul_one]
  convert (hasSum_nat_jacobiTheta this).summable.norm using 1
  ext1 n
  rw [Complex.norm_eq_abs, Complex.abs_exp]
  rw [show â†‘Ï€ * I * ((n : â„‚) + 1) ^ 2 * (â†‘t * I) = ((Ï€ * t * ((n : â„) + 1) ^ 2) : â„) * I ^ 2 by
    push_cast; ring]
  rw [I_sq, mul_neg_one, â† ofReal_neg, ofReal_re, neg_mul, neg_mul]
#align summable_exp_neg_pi_mul_nat_sq summable_exp_neg_pi_mul_nat_sq

theorem mellin_zetaKernel_eq_tsum {s : â„‚} (hs : 1 / 2 < s.re) :
    mellin (fun t â†¦ (zetaKernel t - 1) / 2) s =
    (Ï€ : â„‚) ^ (-s) * Gamma s * âˆ‘' n : â„•, 1 / ((n : â„‚) + 1) ^ (2 * s) := by
  let bd : â„• â†’ â„ â†’ â„ := fun n t â†¦ t ^ (s.re - 1) * exp (-Ï€ * t * ((n : â„) + 1) ^ 2)
  let f : â„• â†’ â„ â†’ â„‚ := fun n t â†¦ (t : â„‚) ^ (s - 1) * exp (-Ï€ * t * ((n : â„) + 1) ^ 2)
  have hm : MeasurableSet (Ioi (0 : â„)) := measurableSet_Ioi
  have h_norm : âˆ€ (n : â„•) {t : â„}, 0 < t â†’ â€–f n tâ€– = bd n t := by
    intro n t ht
    rw [norm_mul, Complex.norm_eq_abs, Complex.norm_eq_abs, Complex.abs_of_nonneg (exp_pos _).le,
      abs_cpow_eq_rpow_re_of_pos ht, sub_re, one_re]
  have hf_meas : âˆ€ n : â„•, AEStronglyMeasurable (f n) (volume.restrict <| Ioi 0) := by
    intro n
    refine (ContinuousOn.mul ?_ ?_).aestronglyMeasurable hm
    Â· exact ContinuousAt.continuousOn fun x hx â†¦
          continuousAt_ofReal_cpow_const _ _ <| Or.inr <| ne_of_gt hx
    Â· apply Continuous.continuousOn
      exact continuous_ofReal.comp
          (continuous_exp.comp ((continuous_const.mul continuous_id').mul continuous_const))
  have h_le : âˆ€ n : â„•, âˆ€áµ t : â„ âˆ‚volume.restrict (Ioi 0), â€–f n tâ€– â‰¤ bd n t := fun n â†¦
    (ae_restrict_iff' hm).mpr (ae_of_all _ fun t ht â†¦ le_of_eq (h_norm n ht))
  have h_sum0 : âˆ€ {t : â„}, 0 < t â†’ HasSum (fun n â†¦ f n t)
      ((t : â„‚) ^ (s - 1) * ((zetaKernel t - 1) / 2)) := by
    intro t ht
    rw [zetaKernel_sub_one_eq_tsum_nat ht, mul_comm (2 : â„‚), mul_div_cancel _ two_ne_zero]
    exact (hasSum_ofReal.mpr (summable_exp_neg_pi_mul_nat_sq ht).hasSum).mul_left _
  have h_sum' : âˆ€áµ t : â„ âˆ‚volume.restrict (Ioi 0), HasSum (fun n : â„• â†¦ f n t)
      ((t : â„‚) ^ (s - 1) * ((zetaKernel t - 1) / 2)) :=
    (ae_restrict_iff' hm).mpr (ae_of_all _ fun t ht â†¦ h_sum0 ht)
  have h_sum : âˆ€áµ t : â„ âˆ‚volume.restrict (Ioi 0), Summable fun n : â„• â†¦ bd n t := by
    refine (ae_restrict_iff' hm).mpr (ae_of_all _ fun t ht â†¦ ?_)
    simpa only [fun n â†¦ h_norm n ht] using (h_sum0 ht).summable.norm
  -- now we show the sum of the bounds is integrable, by reducing to convergence of the Mellin
  -- transform with `re s` in place of `s`
  have h_int0 : MellinConvergent (zetaKernel Â· - 1) (re s) :=
    (zetaKernelFEPair.hasMellin (ofReal_re s.re â–¸ hs)).1
  have h_int : IntegrableOn (âˆ‘' n : â„•, bd n Â·) (Ioi 0)
  Â· refine IntegrableOn.congr_fun (h_int0.div_const 2).re (fun t (ht : 0 < t) â†¦ ?_) hm
    rw [IsROrC.re_eq_complex_re, smul_eq_mul, (by simp : (re s : â„‚) - 1 = â†‘(re s - 1)),
      â† ofReal_cpow ht.le, re_ofReal_mul, tsum_mul_left, mul_right_inj' (rpow_pos_of_pos ht _).ne']
    simp_rw [zetaKernel_sub_one_eq_tsum_nat ht, mul_comm (2 : â„‚), mul_div_cancel _ (two_ne_zero' â„‚),
      ofReal_re]
  simp_rw [â† tsum_mul_left, â† integral_cpow_mul_exp_neg_pi_mul_sq (one_half_pos.trans hs),
    â† (hasSum_integral_of_dominated_convergence bd hf_meas h_le h_sum h_int h_sum').tsum_eq.symm]
  rfl
#align mellin_zeta_kernelâ‚_eq_tsum mellin_zetaKernel_eq_tsum

theorem completedZeta_eq_tsum_of_one_lt_re {s : â„‚} (hs : 1 < re s) :
    riemannCompletedZeta s =
      (Ï€ : â„‚) ^ (-s / 2) * Gamma (s / 2) * âˆ‘' n : â„•, 1 / ((n : â„‚) + 1) ^ s := by
  erw [completedZeta_eq_mellin_of_one_lt_re hs, mellin_zetaKernel_eq_tsum, neg_div,
    mul_div_cancel' _ (two_ne_zero' â„‚)]
  rw [show s / 2 = â†‘(2â»Â¹ : â„) * s by push_cast; rw [mul_comm]; rfl]
  rwa [re_ofReal_mul, â† div_eq_inv_mul, div_lt_div_right (zero_lt_two' â„)]
#align completed_zeta_eq_tsum_of_one_lt_re completedZeta_eq_tsum_of_one_lt_re

/-- The Riemann zeta function agrees with the naive Dirichlet-series definition when the latter
converges. (Note that this is false without the assumption: when `re s â‰¤ 1` the sum is divergent,
and we use a different definition to obtain the analytic continuation to all `s`.) -/
theorem zeta_eq_tsum_one_div_nat_add_one_cpow {s : â„‚} (hs : 1 < re s) :
    riemannZeta s = âˆ‘' n : â„•, 1 / ((n : â„‚) + 1) ^ s := by
  have : s â‰  0 := by contrapose! hs; rw [hs, zero_re]; exact zero_le_one
  rw [riemannZeta, Function.update_noteq this, completedZeta_eq_tsum_of_one_lt_re hs, â† mul_assoc,
    neg_div, cpow_neg, mul_inv_cancel_leftâ‚€, mul_div_cancel_left]
  Â· apply Gamma_ne_zero_of_re_pos
    rw [div_eq_mul_inv, mul_comm, show (2â»Â¹ : â„‚) = (2â»Â¹ : â„) by norm_num, re_ofReal_mul]
    exact mul_pos (inv_pos_of_pos two_pos) (zero_lt_one.trans hs)
  Â· rw [Ne.def, cpow_eq_zero_iff, not_and_or, â† Ne.def, ofReal_ne_zero]
    exact Or.inl pi_pos.ne'
#align zeta_eq_tsum_one_div_nat_add_one_cpow zeta_eq_tsum_one_div_nat_add_one_cpow

/-- Alternate formulation of `zeta_eq_tsum_one_div_nat_add_one_cpow` without the `+ 1`, using the
fact that for `s â‰  0` we define `0 ^ s = 0`.  -/
theorem zeta_eq_tsum_one_div_nat_cpow {s : â„‚} (hs : 1 < re s) :
    riemannZeta s = âˆ‘' n : â„•, 1 / (n : â„‚) ^ s := by
  have hs' : s â‰  0 := by contrapose! hs; rw [hs, zero_re]; exact zero_le_one
  rw [tsum_eq_zero_add]
  Â· simp_rw [Nat.cast_zero, zero_cpow hs', div_zero, zero_add,
      zeta_eq_tsum_one_div_nat_add_one_cpow hs, Nat.cast_add, Nat.cast_one]
  Â· refine .of_norm ?_
    simp_rw [norm_div, norm_one, Complex.norm_eq_abs, â† ofReal_nat_cast,
      abs_cpow_eq_rpow_re_of_nonneg (Nat.cast_nonneg _) (zero_lt_one.trans hs).ne',
      summable_one_div_nat_rpow]
    assumption
#align zeta_eq_tsum_one_div_nat_cpow zeta_eq_tsum_one_div_nat_cpow

/-- Special case of `zeta_eq_tsum_one_div_nat_cpow` when the argument is in `â„•`, so the power
function can be expressed using naÃ¯ve `pow` rather than `cpow`. -/
theorem zeta_nat_eq_tsum_of_gt_one {k : â„•} (hk : 1 < k) :
    riemannZeta k = âˆ‘' n : â„•, 1 / (n : â„‚) ^ k := by
  simp only [zeta_eq_tsum_one_div_nat_cpow
      (by rwa [â† ofReal_nat_cast, ofReal_re, â† Nat.cast_one, Nat.cast_lt] : 1 < re k),
    cpow_nat_cast]
#align zeta_nat_eq_tsum_of_gt_one zeta_nat_eq_tsum_of_gt_one

/-- Explicit formula for `Î¶ (2 * k)`, for `k âˆˆ â„•` with `k â‰  0`: we have
`Î¶ (2 * k) = (-1) ^ (k + 1) * 2 ^ (2 * k - 1) * Ï€ ^ (2 * k) * bernoulli (2 * k) / (2 * k)!`.
Compare `hasSum_zeta_nat` for a version formulated explicitly as a sum, and
`riemannZeta_neg_nat_eq_bernoulli` for values at negative integers (equivalent to the above via
the functional equation). -/
theorem riemannZeta_two_mul_nat {k : â„•} (hk : k â‰  0) :
    riemannZeta (2 * k) = (-1 : â„‚) ^ (k + 1) * (2 : â„‚) ^ (2 * k - 1)
      * (Ï€ : â„‚) ^ (2 * k) * bernoulli (2 * k) / (2 * k)! := by
  convert congr_arg ((â†‘) : â„ â†’ â„‚) (hasSum_zeta_nat hk).tsum_eq
  Â· rw [â† Nat.cast_two, â† Nat.cast_mul, zeta_nat_eq_tsum_of_gt_one]
    Â· rw [ofReal_tsum]
      norm_num
    Â· refine one_lt_two.trans_le ?_
      conv_lhs => rw [â† mul_one 2]
      rwa [mul_le_mul_left (zero_lt_two' â„•), Nat.one_le_iff_ne_zero]
  Â· norm_num
#align riemann_zeta_two_mul_nat riemannZeta_two_mul_nat

theorem riemannZeta_two : riemannZeta 2 = (Ï€ : â„‚) ^ 2 / 6 := by
  convert congr_arg ((â†‘) : â„ â†’ â„‚) hasSum_zeta_two.tsum_eq
  Â· rw [â† Nat.cast_two, zeta_nat_eq_tsum_of_gt_one one_lt_two, ofReal_tsum]
    norm_num
  Â· norm_num
#align riemann_zeta_two riemannZeta_two

theorem riemannZeta_four : riemannZeta 4 = Ï€ ^ 4 / 90 := by
  convert congr_arg ((â†‘) : â„ â†’ â„‚) hasSum_zeta_four.tsum_eq
  Â· rw [â† Nat.cast_one, show (4 : â„‚) = (4 : â„•) by norm_num,
      zeta_nat_eq_tsum_of_gt_one (by norm_num : 1 < 4), ofReal_tsum]
    norm_num
  Â· norm_num
#align riemann_zeta_four riemannZeta_four

theorem riemannZeta_neg_nat_eq_bernoulli (k : â„•) :
    riemannZeta (-k) = (-1 : â„‚) ^ k * bernoulli (k + 1) / (k + 1) := by
  rcases Nat.even_or_odd' k with âŸ¨m, rfl | rflâŸ©
  Â· cases' m with m m
    Â·-- k = 0 : evaluate explicitly
      rw [Nat.zero_eq, mul_zero, Nat.cast_zero, pow_zero, one_mul, zero_add, neg_zero, zero_add,
        div_one, bernoulli_one, riemannZeta_zero]
      norm_num
    Â· -- k = 2 * (m + 1) : both sides "trivially" zero
      rw [Nat.cast_mul, â† neg_mul, Nat.cast_two, Nat.cast_succ, riemannZeta_neg_two_mul_nat_add_one,
        bernoulli_eq_bernoulli'_of_ne_one]
      swap; Â· apply ne_of_gt; norm_num
      rw [bernoulli'_odd_eq_zero âŸ¨m + 1, rflâŸ© (by norm_num), Rat.cast_zero, mul_zero,
        zero_div]
  Â· -- k = 2 * m + 1 : the interesting case
    rw [Odd.neg_one_pow âŸ¨m, rflâŸ©]
    rw [show -(â†‘(2 * m + 1) : â„‚) = 1 - (2 * m + 2) by push_cast; ring]
    rw [riemannZeta_one_sub]
    rotate_left
    Â· intro n
      rw [(by norm_cast : 2 * (m : â„‚) + 2 = â†‘(2 * m + 2)), â† Int.cast_neg_natCast, â† Int.cast_ofNat,
        Ne.def, Int.cast_inj]
      apply ne_of_gt
      exact lt_of_le_of_lt (by norm_num : (-n : â„¤) â‰¤ 0) (by positivity)
    Â· rw [(by norm_cast : 2 * (m : â„‚) + 2 = â†‘(2 * m + 2)), Ne.def, Nat.cast_eq_one]; norm_num
    -- get rid of sine term
    rw [show Complex.sin (â†‘Ï€ * (1 - (2 * â†‘m + 2)) / 2) = -(-1 : â„‚) ^ m by
        rw [(by field_simp; ring : (Ï€ : â„‚) * (1 - (2 * â†‘m + 2)) / 2 = Ï€ / 2 - (Ï€ * m + Ï€))]
        rw [Complex.sin_pi_div_two_sub, Complex.cos_add_pi, neg_inj]
        rcases Nat.even_or_odd' m with âŸ¨t, rfl | rflâŸ©
        Â· rw [pow_mul, neg_one_sq, one_pow]
          convert Complex.cos_nat_mul_two_pi t using 2
          push_cast; ring_nf
        Â· rw [pow_add, pow_one, pow_mul, neg_one_sq, one_pow, one_mul]
          convert Complex.cos_nat_mul_two_pi_add_pi t using 2
          push_cast; ring_nf]
    -- substitute in what we know about zeta values at positive integers
    have step1 := congr_arg ((â†‘) : â„ â†’ â„‚) (hasSum_zeta_nat (by norm_num : m + 1 â‰  0)).tsum_eq
    have step2 := zeta_nat_eq_tsum_of_gt_one (by rw [mul_add]; norm_num : 1 < 2 * (m + 1))
    simp_rw [ofReal_tsum, ofReal_div, ofReal_one, ofReal_pow, ofReal_nat_cast] at step1
    rw [step1, (by norm_cast : (â†‘(2 * (m + 1)) : â„‚) = 2 * â†‘m + 2)] at step2
    rw [step2, mul_div]
    -- now the rest is just a lengthy but elementary rearrangement
    rw [show ((2 * (m + 1))! : â„‚) = Complex.Gamma (2 * m + 2) * (â†‘(2 * m + 1) + 1) by
        rw [(by push_cast; ring : (2 * m + 2 : â„‚) = â†‘(2 * m + 1) + 1),
          Complex.Gamma_nat_eq_factorial, (by ring : 2 * (m + 1) = 2 * m + 1 + 1),
          Nat.factorial_succ, Nat.cast_mul, mul_comm]
        norm_num]
    rw [â† div_div, neg_one_mul]
    congr 1
    rw [div_eq_iff (Gamma_ne_zero_of_re_pos _)]
    swap; Â· rw [(by norm_num : 2 * (m : â„‚) + 2 = â†‘(2 * (m : â„) + 2)), ofReal_re]; positivity
    simp_rw [ofReal_mul, â† mul_assoc, ofReal_rat_cast, mul_add, Nat.add_assoc, mul_one,
      one_add_one_eq_two, mul_neg, neg_mul, neg_inj]
    conv_rhs => rw [mul_comm]
    congr 1
    rw [ofReal_pow, ofReal_neg, ofReal_one, pow_add, neg_one_sq, mul_one]
    conv_lhs =>
      congr
      congr
      rw [mul_assoc, â† pow_add, â† two_mul, pow_mul, neg_one_sq, one_pow, mul_one]
    rw [show (2 : â„‚) ^ (1 - (2 * (m : â„‚) + 2)) = (â†‘((2 : â„) ^ (2 * m + 2 - 1)))â»Â¹ by
        rw [ofReal_pow, â† cpow_nat_cast, â† cpow_neg, show (2 : â„) = (2 : â„‚) by norm_num]
        congr 1
        rw [Nat.add_sub_assoc one_le_two, Nat.cast_add, Nat.cast_mul, Nat.cast_two,
          (by norm_num : 2 - 1 = 1)]
        push_cast; ring]
    rw [show (Ï€ : â„‚) ^ (-(2 * (m : â„‚) + 2)) = (â†‘(Ï€ ^ (2 * m + 2)))â»Â¹ by
        rw [ofReal_pow, â† cpow_nat_cast, â† cpow_neg, Nat.cast_add, Nat.cast_mul, Nat.cast_two]]
    rw [(by intros; ring : âˆ€ a b c d e : â„‚, a * b * c * d * e = a * d * (b * e) * c)]
    rw [inv_mul_cancel (ofReal_ne_zero.mpr <| pow_ne_zero _ pi_pos.ne'),
      inv_mul_cancel (ofReal_ne_zero.mpr <| pow_ne_zero _ two_ne_zero), one_mul, one_mul]
#align riemann_zeta_neg_nat_eq_bernoulli riemannZeta_neg_nat_eq_bernoulli

/-- The residue of `Î›(s)` at `s = 1` is equal to `1`. -/
lemma riemannCompletedZeta_residue_one :
    Tendsto (fun s â†¦ (s - 1) * riemannCompletedZeta s) (ğ“[â‰ ] 1) (ğ“ 1) := by
  have h1 : Tendsto (fun s : â„‚ â†¦ (s - â†‘(1  / 2 : â„)) * _) (ğ“[â‰ ] â†‘(1  / 2 : â„))
    (ğ“ ((1 : â„‚) * (1 : â„‚))) := zetaKernelFEPair.Î›_residue_k
  simp only [push_cast, one_mul] at h1
  have h2 : Tendsto (fun s : â„‚ â†¦ s / 2) (ğ“[â‰ ] 1) (ğ“[â‰ ] (1 / 2)) :=
    le_of_eq ((Homeomorph.mulRightâ‚€ _ (inv_ne_zero (two_ne_zero' â„‚))).map_punctured_nhds_eq 1)
  refine (h1.comp h2).congr (fun x â†¦ ?_)
  rw [riemannCompletedZeta, Function.comp_apply]
  ring_nf

/-- The residue of `Î¶(s)` at `s = 1` is equal to 1. -/
lemma riemannZeta_residue_one : Tendsto (fun s â†¦ (s - 1) * riemannZeta s) (ğ“[â‰ ] 1) (ğ“ 1) := by
  suffices : Tendsto (fun s â†¦ (s - 1) *
      (Ï€ ^ (s / 2) * riemannCompletedZeta s / Gamma (s / 2))) (ğ“[â‰ ] 1) (ğ“ 1)
  Â· refine this.congr' <| (eventually_ne_nhdsWithin one_ne_zero).mp (eventually_of_forall ?_)
    intro x hx
    simp [riemannZeta, Function.update_noteq hx]
  have h0 : Tendsto (fun s â†¦ Ï€ ^ (s / 2) : â„‚ â†’ â„‚) (ğ“[â‰ ] 1) (ğ“ (Ï€ ^ (1 / 2 : â„‚)))
  Â· refine ((continuousAt_id.div_const _).const_cpow ?_).tendsto.mono_left nhdsWithin_le_nhds
    exact Or.inl <| ofReal_ne_zero.mpr pi_ne_zero
  have h1 : Tendsto (fun s : â„‚ â†¦ 1 / Gamma (s / 2)) (ğ“[â‰ ] 1) (ğ“ (1 / Ï€ ^ (1 / 2 : â„‚)))
  Â· rw [â† Complex.Gamma_one_half_eq]
    refine (Continuous.tendsto ?_ _).mono_left nhdsWithin_le_nhds
    simpa using differentiable_one_div_Gamma.continuous.comp (continuous_id.div_const _)
  convert (riemannCompletedZeta_residue_one.mul h0).mul h1 using 2 with s
  Â· ring
  Â· have := fun h â†¦ ofReal_ne_zero.mpr pi_ne_zero ((cpow_eq_zero_iff _ (1 / 2)).mp h).1
    field_simp
